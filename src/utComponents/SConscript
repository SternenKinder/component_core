# HOW TO BUILD (A) LIBRAR-Y/IES FROM A MODULE
# Building libraries from modules should be consistent for each module.
# Use the following scheme for each new library created:
#
# a) Check for required libraries. If they are not available return False and export flags if needed
# b) Define the src-subdirectories for which the library should be compiled
#    and glob all files in there
# c) Define a [LIBRARY]_options variable for the library containing all dependencies
#    from other libraries. Create a clone from the master environment and add those options.
# d) WINDOWS ONLY: Create a header file which implements dll interface functionality
#    dependent from a [LIBRARY]_DLL define. If the define is set, the dll should be exported,
#    if not, the dll should be imported. Set the define in the preprocessor options for the environment.
# e) Build the library!
# f) Remove all entries for (at least) the LIBS and LIBPATH keys in the options and
#    add only the newly-compiled library as a dependcy. This will avoid multiple includes
#    of libraries. Also verify that the import/export-switch define is not set.
# g) Export the options so that other libraries can base on this library. 
# h) Optionally setup help and ide projects
#
# The use of options and possibility to export them makes hierarchical build environments
# obsolete. Avoid exporting new environments to the build system.

import sys
import glob
import os.path

# import variables from other scons files
have_utdataflow = False
Import( '*' )

# a)
if not have_utdataflow:
	print "Dataflow module missing -- not building components"
	result = False
	Return("result")
	


# b)
comp_sources = globSourceFiles( '*.cpp' )

# remove components that require a not present feature
if not have_lapack:
	for src in [ 'RotHecKalmanFilter.cpp', 'RotOnlyKalmanFilter.cpp', 'HomogeneousMatrixEstimation.cpp', 'DecomposeProjectionMatrix.cpp', 'SPAAM.cpp', 'AbsoluteOrientation.cpp', 'AbsoluteOrientationRANSAC.cpp', 'OnlineRotHec.cpp', '2D3DPoseEstimation.cpp', 'HandEyeCalibration.cpp', 'PoseKalmanFilter.cpp', 'TipCalibration.cpp', 'MultipleCameraPoseOptimization.cpp' ]:
		comp_sources.remove( src );


# c)	
component_options = mergeOptions( utdataflow_all_options)
env = masterEnv.Clone()
env.AppendUnique( **component_options )
		
# d)
# library is component, nothing to export

# e)	
# compile every single source files its own library
# {buildenvironment, source files, build target}
libs = setupComponentBuild(env, comp_sources, 'components')

# f)
utcomponents_options = {}
utcomponents_options[ 'CPPPATH' ] =  [ os.path.join( getCurrentPath(), '..') ]
utcomponents_options[ 'LIBS' ] = []
utcomponents_options[ 'LIBPATH' ] = [ install_library_prefix ]


# g)
have_utcomponents = libs != 0
Export( 'utcomponents_options', 'have_utcomponents' )

# h)
sources = globSourceFiles( '*.cpp' )
headers = globSourceFiles( '*.h' )
createVisualStudioProject(env, sources, headers, 'utComponents')
